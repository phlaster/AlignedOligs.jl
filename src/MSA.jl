abstract type AbstractMSA end

struct MSA <: AbstractMSA
    seqs::Vector{GappedOlig}
    base_count::Matrix{Float64}
    bootstrap::Int

    function MSA(seqs::Vector{<:GappedOlig}; bootstrap::Int=0, seed=nothing)
        bootstrap >= 0 || throw(ArgumentError("bootstrap must be non-negative"))
        isnothing(seed) || Random.seed!(seed)

        isempty(seqs) && return new(seqs, zeros(4, 0), bootstrap)

        L = length(first(seqs))
        all(length(s) == L for s in seqs) || throw(ArgumentError("All gapped sequences must have the same length"))
        n = length(seqs)

        base_count = zeros(4, L)
        _compute_base_counts!(base_count, seqs, bootstrap, n, L; progress_label="Bootstrap, $bootstrap it.", barlen=19)
        return new(seqs, base_count, bootstrap)
    end
end

function _compute_base_counts!(base_count::Matrix{Float64}, seqs::Vector{<:GappedOlig}, bootstrap::Int, n::Int, L::Int; progress_label::String, barlen::Int)
    if bootstrap > 0
        @showprogress desc=progress_label barlen=barlen for b in 1:bootstrap
            boot_rows = rand(1:n, n)
            boot_seqs = seqs[boot_rows]
            
            Threads.@threads for j in 1:L
                pos_counts = zeros(4)
                for s in boot_seqs
                    c = s[j]
                    probs = IUPAC_PROBS[c]
                    pos_counts .+= probs
                end
                current_freq = pos_counts / n
                base_count[:, j] += (current_freq - base_count[:, j]) / b
            end
        end
    else
        Threads.@threads for j in 1:L
            counts = zeros(4)
            for s in seqs
                c = s[j]
                probs = IUPAC_PROBS[c]
                counts .+= probs
            end
            base_count[:, j] = counts / n
        end
    end
end

struct MSAView <: AbstractMSA
    parent::AbstractMSA
    rows::UnitRange{Int}
    cols::UnitRange{Int}
end

root(msa::MSA) = msa
root(msav::MSAView) = root(msav.parent)
bval(msa::MSA) = msa.bootstrap
bval(msav::MSAView) = root(msav).bootstrap
# min_tresh(msa::MSA) = msa.minor_threshold
# min_tresh(msav::MSAView) = root(msav).minor_threshold
_is_full_height(msa::MSA) = true
_is_full_height(msav::MSAView) = msav.rows == 1:nseqs(root(msav))

function _align!(::Vector{Tuple{String,String}})
    # This is overloaded in ext/MAFFTExt.jl to load MAFFT_jll artifact dynamically
    error("Aignment requires MAFFT_jll artifact to be installed.\n\
        If you need to align your FASTAs, please `]add MAFFT_jll` to your project\n\
        and load it with `using MAFFT_jll` before calling MSA with `mafft=true`."
    )
end

function MSA(predicate::Function, fasta::AbstractString; mafft::Bool=false, bootstrap::Int=0, seed=nothing)
    fasta_content = Tuple{String, String}[]
    FastaReader(fasta) do fr
        counter = 0
        for (desc, seq) in fr
            predicate(desc) ? (counter += 1) : continue
            desc = isempty(desc) ? "seq$counter" : desc
            push!(fasta_content, (desc, seq))
        end
    end

    allowed_chars = mafft ? (NON_DEGEN_BASES..., 'N') : (NON_DEGEN_BASES..., '-', 'N')
    for (desc, seq) in fasta_content
        upper_seq = uppercase(seq)
        if !all(in(allowed_chars), upper_seq)
            invalid_chars = setdiff(unique(upper_seq), collect(allowed_chars))
            throw(ArgumentError(
                "Sequence '$desc' contains invalid characters: $(join(invalid_chars, ", ")).\n\
                Only $(join(collect(allowed_chars), ", ")) allowed when `mafft=$mafft`"
            ))
        end
    end

    mafft && _align!(fasta_content)

    gapped_oligs = [GappedOlig(seq, desc) for (desc, seq) in fasta_content]
    return MSA(gapped_oligs; bootstrap=bootstrap, seed=seed)
end
MSA(fasta::AbstractString; kwargs...) = MSA(x->true, fasta; kwargs...)

function Base.getindex(msa::AbstractMSA, rows::UnitRange{Int}, cols::UnitRange{Int})
    root_msa = root(msa)
    abs_rows = isa(msa, MSA) ? rows : (msa.rows.start + rows.start - 1):(msa.rows.start + rows.stop - 1)
    abs_cols = isa(msa, MSA) ? cols : (msa.cols.start + cols.start - 1):(msa.cols.start + cols.stop - 1)
    return MSAView(root_msa, abs_rows, abs_cols)
end
Base.getindex(msa::AbstractMSA, row::Int, col::Int) = getsequence(msa, row, col)
Base.getindex(msa::AbstractMSA, row::Int, cols::UnitRange{Int}) = msa[row:row, cols]
Base.getindex(msa::AbstractMSA, row::Int, cols::Colon) = getsequence(msa, row)
Base.getindex(msa::AbstractMSA, rows::UnitRange{Int}, col::Int) = msa[rows, col:col]
Base.getindex(msa::AbstractMSA, rows::Colon, col::Int) = msa[:, col:col]
Base.getindex(msa::AbstractMSA, rows::Colon, cols::UnitRange{Int}) = msa[1:nseqs(msa), cols]
Base.getindex(msa::AbstractMSA, rows::UnitRange{Int}, cols::Colon) = msa[rows, 1:length(msa)]
Base.getindex(msa::AbstractMSA, rows::Colon, cols::Colon) = msa[1:nseqs(msa), 1:length(msa)]

function Base.checkbounds(msa::AbstractMSA, rows::Colon, cols::UnitRange{<:Integer})
    if ! (1 <= cols.start <= cols.stop <= length(msa))
        throw(BoundsError(msa, (:, cols)))
    end
end
function Base.checkbounds(msa::AbstractMSA, rows::UnitRange{<:Integer}, cols::Colon)
    if ! (1 <= rows.start <= rows.stop <= nseqs(msa))
        throw(BoundsError(msa, (rows, :)))
    end
end
function Base.checkbounds(msa::AbstractMSA, rows::UnitRange{<:Integer}, cols::UnitRange{<:Integer})
    if ! (1 <= rows.start <= rows.stop <= nseqs(msa))
        throw(BoundsError(msa, (rows, cols)))
    end
    if ! (1 <= cols.start <= cols.stop <= length(msa))
        throw(BoundsError(msa, (rows, cols)))
    end
end
function Base.checkbounds(::AbstractMSA, ::Colon, ::Colon) end

nseqs(msa::MSA) = length(msa.seqs)
nseqs(v::MSAView) = length(v.rows)

Base.length(msa::MSA) = size(msa.base_count, 2)
Base.length(v::MSAView) = length(v.cols)

# aliases
width(msa::AbstractMSA) = length(msa)
height(msa::AbstractMSA) = nseqs(msa)

Base.ndims(::AbstractMSA) = 2

Base.size(msa::AbstractMSA) = (nseqs(msa), length(msa))

Base.size(msa::AbstractMSA, dim::Int) =
    if dim == 1
        nseqs(msa)
    elseif dim == 2
        length(msa)
    else
        throw(ArgumentError("AbstractMSA only has dimensions 1 and 2"))
    end

Base.axes(msa::AbstractMSA, dim::Int) = Base.OneTo(size(msa, dim))

Base.lastindex(msa::AbstractMSA, dim::Int) = size(msa, dim)
Base.lastindex(msa::AbstractMSA) = lastindex(msa, ndims(msa))

getsequence(msa::MSA, row::Int) = msa.seqs[row]
function getsequence(v::MSAView, row::Int)
    abs_row = v.rows.start + row - 1
    parent_seq = getsequence(root(v), abs_row)
    return parent_seq[v.cols]
end
getsequence(msa::AbstractMSA, row::Int, col::Int) = getsequence(msa, row)[col]

get_base_count(msa::MSA, pos::Int) = @view msa.base_count[:, pos]
get_base_count(msa::MSA, interval::UnitRange{Int}) = @view msa.base_count[:, interval]
get_base_count(msa::MSA) = msa.base_count
function get_base_count(msav::MSAView, pos::Int)
    if !_is_full_height(msav)
        throw(ErrorException("get_base_count not supported for views that slice rows (height), as it requires recomputation"))
    end
    abs_pos = msav.cols.start + pos - 1
    return @view root(msav).base_count[:, abs_pos]
end
function get_base_count(msav::MSAView, interval::UnitRange{Int})
    if !_is_full_height(msav)
        throw(ErrorException("get_base_count not supported for views that slice rows (height), as it requires recomputation"))
    end
    abs_start = msav.cols.start + interval.start - 1
    abs_interval = abs_start:(abs_start + length(interval) - 1)
    return @view root(msav).base_count[:, abs_interval]
end
function get_base_count(msav::MSAView)
    return get_base_count(msav, 1:length(msav))
end

function msadepth(msa::AbstractMSA, pos::Int)::Float64
    min(1.0, sum(view(msa.base_count, :, pos)))
end
function msadepth(msa::AbstractMSA, interval::UnitRange{Int})::Vector{Float64}
    min.(1.0, sum(view(msa.base_count, :, interval), dims=1))
end
function msadepth(msa::AbstractMSA)::Vector{Float64}
    return msadepth(msa, 1:length(msa))
end

function msadet(msa::AbstractMSA, pos::Int)::Float64
    v = get_base_count(msa, pos)
    s = min(1.0, sum(v))
    s == 0.0 ? 0.0 : maximum(v) / s
end
function msadet(msa::AbstractMSA, interval::UnitRange{Int})::Vector{Float64}
    return [msadet(msa, pos) for pos in interval]
end
function msadet(msa::AbstractMSA)::Vector{Float64}
    return msadet(msa, 1:length(msa))
end

function consensus_major(msa::AbstractMSA, pos::Int)
    p = get_base_count(msa, pos)
    if sum(p) == 0
        return '-'
    end
    return NON_DEGEN_BASES[argmax(p)]
end
function consensus_major(msa::AbstractMSA, interval::UnitRange{Int}=1:width(msa))
    seq = join(consensus_major(msa, j) for j in interval)
    desc = "Major consensus for $(nseqs(msa)) seq MSA"
    return GappedOlig(seq, desc)
end

function consensus_degen(msa::AbstractMSA, pos::Int; slack::Real=0.0)
    0 ≤ slack < 1 || throw(ArgumentError("slack must be in [0,1)"))
    p = get_base_count(msa, pos)
    if sum(p) == 0
        return '-'
    end
    active = findall(>(slack), p)
    isempty(active) && return '-'
    bs = sort!(collect(NON_DEGEN_BASES[active]))
    return IUPAC_V2B[bs]
end

function consensus_degen(msa::AbstractMSA, interval::UnitRange{Int}=1:width(msa); slack::Real=0.0)
    seq = join(consensus_degen(msa, j; slack=slack) for j in interval)
    desc = "Degenerate consensus for $(nseqs(msa)) seq MSA"
    return GappedOlig(seq, desc)
end

function dry_msa(msa::AbstractMSA; gap_content::Real=1.0)
    0 ≤ gap_content ≤ 1 || throw(ArgumentError("gap_content must be in [0,1]"))
    non_gap_cols = [j for j in 1:length(msa) if any(>(0.0), get_base_count(msa, j))]
    if isempty(non_gap_cols)
        return msa[:, 1:0]
    end
    num_cols = length(non_gap_cols)
    kept_rows = Int[]
    for i in 1:nseqs(msa)
        gap_count = sum((1 for j in non_gap_cols if getsequence(msa, i, j) == '-'), init=0)
        prop = num_cols > 0 ? gap_count / num_cols : 0.0
        if prop < gap_content
            push!(kept_rows, i)
        end
    end
    if isempty(kept_rows)
        return MSA(GappedOlig[]; bootstrap=bval(msa))
    end
    new_seqs = Vector{GappedOlig}(undef, length(kept_rows))
    for (k, row) in enumerate(kept_rows)
        sub_str = join(getsequence(msa, row, j) for j in non_gap_cols)
        new_seqs[k] = GappedOlig(sub_str, description(getsequence(msa, row)))
    end
    return MSA(new_seqs; bootstrap=bval(msa))
end

function nucleotide_diversity(msa::AbstractMSA; ignore_gaps::Bool=true, max_pairs::Int=10000)::Float64
    L = length(msa)
    L == 0 && return 0.0
    n = nseqs(msa)
    n < 2 && return 0.0

    total_possible = n * (n - 1) ÷ 2
    compute_all = n <= 200 || max_pairs >= total_possible
    npairs = compute_all ? total_possible : min(max_pairs, total_possible)

    pairs = Vector{Tuple{Int,Int}}(undef, npairs)
    @inbounds if compute_all
        idx = 1
        for i in 1:n-1, j in i+1:n
            pairs[idx] = (i, j)
            idx += 1
        end
    else
        for idx in 1:npairs
            i, j = rand(1:n), rand(1:n)
            while i == j
                i, j = rand(1:n), rand(1:n)
            end
            pairs[idx] = minmax(i, j)
        end
    end

    total_diffs = Threads.Atomic{Float64}(0.0)
    Threads.@threads for (i, j) in pairs
        d = _pairwise_distance(msa, i, j; ignore_gaps)
        Threads.atomic_add!(total_diffs, d)
    end

    return total_diffs[] / npairs
end


function _pairwise_distance(msa::AbstractMSA, i::Int, j::Int; ignore_gaps::Bool=true)::Float64
    seq_i = getsequence(msa, i)
    seq_j = getsequence(msa, j)
    total_sites = 0
    diff_sum = 0.0
    for k in 1:length(msa)
        c_i = seq_i[k]
        c_j = seq_j[k]
        if c_i == '-' || c_j == '-' 
            ignore_gaps && continue
            # Treat gap as mismatch
            total_sites += 1
            diff_sum += 1.0
            continue
        end
        total_sites += 1
        if c_i == c_j
            continue  # Exact match
        end
        # Probabilistic mismatch for IUPAC/degenerate
        probs_i = IUPAC_PROBS[c_i]
        probs_j = IUPAC_PROBS[c_j]
        match_prob = sum(probs_i .* probs_j)
        diff_sum += 1.0 - match_prob
    end
    total_sites == 0 ? 0.0 : diff_sum / total_sites
end
